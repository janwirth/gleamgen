import gleam/bool
import gleam/int
import gleam/io
import gleam/list
import gleam/option
import gleam/result
import gleam/string
import gleamgen/expression
import gleamgen/expression/block
import gleamgen/expression/case_
import gleamgen/expression/constructor
import gleamgen/expression/statement
import gleamgen/function
import gleamgen/import_
import gleamgen/internal/render
import gleamgen/module
import gleamgen/module/definition
import gleamgen/parameter
import gleamgen/pattern
import gleamgen/render/config
import gleamgen/render/report
import gleamgen/types
import gleamgen/types/custom
import gleamgen/types/variant
import gleeunit

pub fn main() {
  gleeunit.main()
}

pub fn simple_int_addition_test() {
  let result =
    expression.int(3)
    |> expression.math_operator(expression.Add, expression.int(5))
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "3 + 5"

  assert result == expected
}

pub fn simple_string_test() {
  let result =
    expression.string("hello")
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "\"hello\""

  assert result == expected
}

pub fn string_escape_quote_test() {
  let result =
    expression.string("hel\"lo")
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "\"hel\\\"lo\""

  assert result == expected
}

pub fn string_escape_slash_test() {
  let result =
    expression.string("hello\\hi")
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "\"hello\\hi\""

  assert result == expected
}

pub fn empty_string_test() {
  let result =
    expression.string("")
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "\"\""

  assert result == expected
}

pub fn simple_tuple_test() {
  let result =
    expression.tuple3(
      expression.int(3),
      expression.string("hello"),
      expression.bool(True),
    )
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "#(3, \"hello\", True)"

  assert result == expected
}

pub fn simple_list_test() {
  let result =
    expression.list([expression.string("hello"), expression.string("hi")])
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "[\"hello\", \"hi\"]"

  assert result == expected
}

pub fn simple_list_prepending_test() {
  let result =
    expression.list_prepend(
      [expression.string("hello"), expression.string("hi")],
      expression.list([expression.string("yo")]),
    )
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "[\"hello\", \"hi\", ..[\"yo\"]]"

  assert result == expected
}

pub fn multiline_list_prepending_test() {
  let result =
    expression.list_prepend(
      [
        expression.string(
          "hello but much, much, much, much longer so it breaks",
        ),
        expression.string(
          "hello but much, much, much, much longer so it breaks",
        ),
      ],
      expression.list([expression.string("yo")]),
    )
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "[
  \"hello but much, much, much, much longer so it breaks\",
  \"hello but much, much, much, much longer so it breaks\",
  ..[\"yo\"]
]"

  assert result == expected
}

pub fn long_list_test() {
  let result =
    expression.list([
      expression.list([
        expression.string("hello"),
        expression.string("hello"),
        expression.string("hello"),
        expression.string("hello"),
        expression.string(
          "hello but much, much, much, much longer so it breaks",
        ),
      ]),
      expression.list([
        expression.string("hi"),
        expression.string("hi"),
        expression.string("hi"),
        expression.string("hi"),
      ]),
    ])
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "[
  [
    \"hello\",
    \"hello\",
    \"hello\",
    \"hello\",
    \"hello but much, much, much, much longer so it breaks\",
  ],
  [\"hi\", \"hi\", \"hi\", \"hi\"],
]"

  assert result == expected
}

pub fn long_tuple_test() {
  let result =
    expression.tuple9(
      expression.int(3),
      expression.string(
        "hello there (making this really long like really long)",
      ),
      expression.bool(True),
      expression.bool(True),
      expression.bool(True),
      expression.bool(True),
      expression.bool(True),
      expression.bool(True),
      expression.bool(True),
    )
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "#(
  3,
  \"hello there (making this really long like really long)\",
  True,
  True,
  True,
  True,
  True,
  True,
  True,
)"

  assert result == expected
}

pub fn simple_todo_test() {
  let result =
    expression.todo_(option.Some("some unimplemented thing"))
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "todo as \"some unimplemented thing\""

  assert result == expected
}

pub fn echo_without_as_test() {
  let result =
    expression.echo_(expression.int(3), option.None)
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "echo 3"

  assert result == expected
}

pub fn echo_with_as_test() {
  let result =
    expression.echo_(expression.int(3), option.Some("should be 3"))
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "echo 3 as \"should be 3\""

  assert result == expected
}

pub fn simple_assert_test() {
  let condition =
    expression.equals(
      expression.int(2),
      expression.math_operator(
        expression.int(5),
        expression.Sub,
        expression.int(3),
      ),
    )
  let result =
    expression.assert_(condition, option.Some("5 - 3 is 2"))
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "assert 2 == 5 - 3 as \"5 - 3 is 2\""

  assert result == expected
}

pub fn simple_float_subtraction_test() {
  let result =
    expression.float(3.3)
    |> expression.math_operator_float(expression.Add, expression.float(5.3))
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "3.3 +. 5.3"

  assert result == expected
}

pub fn equals_test() {
  let result =
    expression.string("hi")
    |> expression.equals(expression.string("hello"))
    |> expression.equals(expression.bool(True))
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "\"hi\" == \"hello\" == True"

  assert result == expected
}

pub fn simple_string_addition_test() {
  let result =
    expression.string("hello ")
    |> expression.concat_string(expression.string("world"))
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected = "\"hello \" <> \"world\""

  assert result == expected
}

pub fn simple_case_string_test() {
  let result =
    case_.new(expression.string("hello"))
    |> case_.with_pattern(pattern.string_literal("hello"), fn(_) {
      expression.string("world")
    })
    |> case_.with_pattern(pattern.variable("v"), fn(v) {
      expression.concat_string(v, expression.string(" world"))
    })
    |> case_.build_expression()
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "case \"hello\" {
  \"hello\" -> \"world\"
  v -> v <> \" world\"
}"

  assert result == expected
}

pub fn simple_case_or_test() {
  let result =
    case_.new(expression.string("hello"))
    |> case_.with_pattern(
      pattern.or(pattern.string_literal("hello"), pattern.string_literal("hi")),
      fn(_) { expression.string("world") },
    )
    |> case_.with_pattern(pattern.variable("v"), fn(v) {
      expression.concat_string(v, expression.string(" world"))
    })
    |> case_.build_expression()
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "case \"hello\" {
  \"hello\" | \"hi\" -> \"world\"
  v -> v <> \" world\"
}"

  assert result == expected
}

pub fn simple_case_as_test() {
  let result =
    case_.new(expression.string("hello"))
    |> case_.with_pattern(
      pattern.string_literal("hello")
        |> pattern.as_("greeting"),
      fn(greeting) {
        expression.concat_string(greeting, expression.string("world"))
      },
    )
    |> case_.with_pattern(pattern.variable("v"), fn(v) {
      expression.concat_string(v, expression.string(" world"))
    })
    |> case_.build_expression()
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "case \"hello\" {
  \"hello\" as greeting -> greeting <> \"world\"
  v -> v <> \" world\"
}"

  assert result == expected
}

pub fn simple_string_concat_test() {
  let result =
    case_.new(expression.string("I love gleam"))
    |> case_.with_pattern(
      pattern.concat_string(starting: "I love ", variable: "thing"),
      fn(thing) {
        expression.string("I love ")
        |> expression.concat_string(thing)
        |> expression.concat_string(expression.string(" too"))
      },
    )
    |> case_.with_pattern(pattern.variable("_"), fn(_) {
      expression.string("Interesting")
    })
    |> case_.build_expression()
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "case \"I love gleam\" {
  \"I love \" <> thing -> \"I love \" <> thing <> \" too\"
  _ -> \"Interesting\"
}"

  assert result == expected
}

pub fn simple_case_tuple_to_multiple_subjects_test() {
  let result =
    case_.new(expression.tuple2(expression.string("hello"), expression.int(3)))
    |> case_.with_pattern(
      pattern.tuple2(
        pattern.string_literal("hello"),
        pattern.named_discard("other"),
      ),
      fn(_) { expression.string("world") },
    )
    |> case_.with_pattern(pattern.discard(), fn(_: Nil) {
      expression.string("other")
    })
    |> case_.build_expression()
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "case \"hello\", 3 {
  \"hello\", _other -> \"world\"
  _, _ -> \"other\"
}"

  assert result == expected
}

pub fn simple_case_tuple_not_multiple_subjects_test() {
  let result =
    case_.new(expression.tuple2(expression.string("hello"), expression.int(3)))
    |> case_.with_pattern(
      pattern.tuple2(pattern.string_literal("hello"), pattern.variable("num")),
      fn(patterns) {
        let #(_, num) = patterns
        expression.tuple2(
          expression.string("world"),
          expression.math_operator(num, expression.Add, expression.int(2)),
        )
      },
    )
    |> case_.with_pattern(
      pattern.variable("my_favorite_variable"),
      fn(my_favorite_variable) { my_favorite_variable },
    )
    |> case_.build_expression()
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "case #(\"hello\", 3) {
  #(\"hello\", num) -> #(\"world\", num + 2)
  my_favorite_variable -> my_favorite_variable
}"

  assert result == expected
}

pub fn simple_case_tuple_to_multiple_subjects_multiple_vars_test() {
  let result =
    case_.new(expression.tuple2(expression.string("hello"), expression.int(3)))
    |> case_.with_pattern(
      pattern.tuple2(pattern.string_literal("hello"), pattern.variable("num")),
      fn(patterns) {
        let #(_, num) = patterns
        expression.tuple2(
          expression.string("world"),
          expression.math_operator(num, expression.Add, expression.int(2)),
        )
      },
    )
    |> case_.with_pattern(
      pattern.tuple2(pattern.variable("greeting"), pattern.variable("num")),
      fn(patterns) {
        let #(greeting, num) = patterns
        expression.tuple2(
          greeting,
          expression.math_operator(num, expression.Sub, expression.int(2)),
        )
      },
    )
    |> case_.build_expression()
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "case \"hello\", 3 {
  \"hello\", num -> #(\"world\", num + 2)
  greeting, num -> #(greeting, num - 2)
}"

  assert result == expected
}

pub fn simple_case_merge_repeated_test() {
  let result =
    case_.new(expression.string("hello"))
    |> case_.with_pattern(
      pattern.or(pattern.string_literal("hello"), pattern.string_literal("hi")),
      fn(_) { expression.string("...world!") },
    )
    |> case_.with_pattern(pattern.string_literal("hola"), fn(_) {
      expression.string("...world!")
    })
    |> case_.with_pattern(pattern.string_literal("labas"), fn(_) {
      expression.string("...world!")
    })
    |> case_.with_pattern(pattern.string_literal("sveiks"), fn(_) {
      expression.string("Latvian????")
    })
    |> case_.with_pattern(pattern.variable("v"), fn(v) {
      expression.concat_string(v, expression.string(" world"))
    })
    |> case_.build_expression()
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "case \"hello\" {
  \"hello\" | \"hi\" | \"hola\" | \"labas\" -> \"...world!\"
  \"sveiks\" -> \"Latvian????\"
  v -> v <> \" world\"
}"

  assert result == expected
}

pub fn case_merge_repeated_test() {
  let result =
    case_.new(expression.string("hello"))
    |> case_.with_pattern(
      pattern.concat_string(starting: "I love ", variable: "thing"),
      fn(thing) {
        expression.concat_string(thing, expression.string("is good!"))
      },
    )
    |> case_.with_pattern(
      pattern.concat_string(
        starting: "My favorite thing is ",
        variable: "thing",
      ),
      fn(thing) {
        expression.concat_string(thing, expression.string("is good!"))
      },
    )
    |> case_.with_pattern(pattern.variable("_"), fn(_) {
      expression.string("I don't know!")
    })
    |> case_.build_expression()
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "case \"hello\" {
  \"I love \" <> thing | \"My favorite thing is \" <> thing -> thing <> \"is good!\"
  _ -> \"I don't know!\"
}"

  assert result == expected
}

pub fn case_merge_repeated_config_disabled_test() {
  let result =
    case_.new(expression.string("hello"))
    |> case_.with_pattern(
      pattern.concat_string(starting: "I love ", variable: "thing"),
      fn(thing) {
        expression.concat_string(thing, expression.string("is good!"))
      },
    )
    |> case_.with_pattern(
      pattern.concat_string(
        starting: "My favorite thing is ",
        variable: "thing",
      ),
      fn(thing) {
        expression.concat_string(thing, expression.string("is good!"))
      },
    )
    |> case_.with_pattern(pattern.variable("_"), fn(_) {
      expression.string("I don't know!")
    })
    |> case_.build_expression()
    |> expression.render(render.context_from_config(
      config.Config(..config.default_config, combine_equivalent_branches: False),
    ))
    |> render.to_string()

  let expected =
    "case \"hello\" {
  \"I love \" <> thing -> thing <> \"is good!\"
  \"My favorite thing is \" <> thing -> thing <> \"is good!\"
  _ -> \"I don't know!\"
}"

  assert result == expected
}

pub fn simple_block_test() {
  let result =
    {
      use x <- block.with_let_declaration("x", expression.int(4))
      use y <- block.with_let_declaration(
        "y",
        expression.math_operator(x, expression.Add, expression.int(5)),
      )
      y
    }
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "{
  let x = 4
  let y = x + 5
  y
}"

  assert result == expected
}

pub fn let_declaration_with_type_test() {
  let result =
    {
      use x <- block.with_let_declaration("x", expression.int(4))
      expression.with_render_config(
        {
          use y <- block.with_let_declaration(
            "y",
            expression.math_operator(x, expression.Add, expression.int(5)),
          )
          y
        },
        config.Config(
          ..config.default_config,
          annotate_type_in_let_declarations: True,
        ),
      )
    }
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "{
  let x = 4
  let y: Int = x + 5
  y
}"

  assert result == expected
}

pub fn block_with_comment_test() {
  let result =
    {
      use x <- block.with_let_declaration("x", expression.int(4))
      use <- block.with_comments(["should be 9"])
      use y <- block.with_let_declaration(
        "y",
        expression.math_operator(x, expression.Add, expression.int(5)),
      )
      y
    }
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "{
  let x = 4
  // should be 9
  let y = x + 5
  y
}"

  assert result == expected
}

pub fn block_with_empty_line_test() {
  let result =
    {
      use x <- block.with_let_declaration("x", expression.int(4))
      use <- block.with_empty_line()
      use <- block.with_comments(["should be 9"])
      use y <- block.with_let_declaration(
        "y",
        expression.math_operator(x, expression.Add, expression.int(5)),
      )
      use <- block.with_empty_line()
      y
    }
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "{
  let x = 4

  // should be 9
  let y = x + 5

  y
}"

  assert result == expected
}

pub fn block_with_invalid_multiple_empty_lines_test() {
  let result =
    {
      use x <- block.with_let_declaration("x", expression.int(4))
      use <- block.with_empty_line()
      use <- block.with_empty_line()
      use <- block.with_empty_line()
      use <- block.with_comments(["should be 9"])
      use y <- block.with_let_declaration(
        "y",
        expression.math_operator(x, expression.Add, expression.int(5)),
      )
      use <- block.with_empty_line()
      use <- block.with_empty_line()
      y
    }
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "{
  let x = 4

  // should be 9
  let y = x + 5

  y
}"

  assert result == expected
}

pub fn block_with_empty_lines_at_start_and_end_test() {
  let result =
    {
      use <- block.with_empty_line()
      use x <- block.with_let_declaration("x", expression.int(4))
      use <- block.with_empty_line()
      use <- block.with_comments(["should be 9"])
      use y <- block.with_let_declaration(
        "y",
        expression.math_operator(x, expression.Add, expression.int(5)),
      )
      use <- block.with_empty_line()
      y
    }
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "{
  let x = 4

  // should be 9
  let y = x + 5

  y
}"

  assert result == expected
}

pub fn block_in_function_test() {
  let block_expr = {
    use x <- block.with_let_declaration("x", expression.int(4))
    use y <- block.with_let_declaration(
      "y",
      expression.math_operator(x, expression.Add, expression.int(5)),
    )
    y
  }

  let result =
    function.new0(types.int, fn() { block_expr })
    |> function.to_dynamic()
    |> function.render(render.default_context(), option.Some("test_function"))
    |> render.to_string()

  let expected =
    "fn test_function() -> Int {
  let x = 4
  let y = x + 5
  y
}"

  assert result == expected
}

pub fn simple_anonymous_function_test() {
  let result =
    {
      use func <- block.with_let_declaration(
        "func",
        function.anonymous(
          function.new2(
            parameter.new("x", types.int),
            parameter.new("y", types.int),
            types.int,
            handler: fn(x, y) { expression.math_operator(x, expression.Add, y) },
          ),
        ),
      )
      expression.call2(func, expression.int(2), expression.int(3))
    }
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "{
  let func = fn(x: Int, y: Int) -> Int { x + y }
  func(2, 3)
}"

  assert result == expected
}

pub fn simple_module_test() {
  let mod = {
    use _based_number <- module.with_constant(
      definition.new("based_number") |> definition.with_publicity(True),
      expression.int(46),
    )
    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected = "pub const based_number = 46"

  assert result == expected
}

pub fn block_with_let_matching_test() {
  let result =
    {
      use x <- block.with_let_declaration("x", expression.ok(expression.int(4)))
      use y <- block.with_matching_let_declaration(
        pattern.or(
          pattern.ok(pattern.variable("y")),
          pattern.error(pattern.variable("y")),
        ),
        x,
        False,
      )
      expression.math_operator(y, expression.Add, expression.int(3))
    }
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "{
  let x = Ok(4)
  let Ok(y) | Error(y) = x
  y + 3
}"

  assert result == expected
}

pub fn block_dynamic_contents_test() {
  let statements =
    list.range(0, 3)
    |> list.map(fn(index) {
      let #(arguments, callback_arguments) =
        list.range(0, index)
        |> list.map(fn(arg_index) {
          let argument = expression.to_dynamic(expression.int(arg_index))
          #(argument, "_callback" <> int.to_string(arg_index))
        })
        |> list.unzip()
      statement.dynamic_use(
        expression.raw("with_args" <> int.to_string(index)),
        arguments,
        callback_arguments,
      )
    })

  let result =
    {
      use <- block.with_statements(statements)
      expression.nil()
    }
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "{
  use _callback0 <- with_args0(0)
  use _callback0, _callback1 <- with_args1(0, 1)
  use _callback0, _callback1, _callback2 <- with_args2(0, 1, 2)
  use _callback0, _callback1, _callback2, _callback3 <- with_args3(0, 1, 2, 3)
  Nil
}"

  assert result == expected
}

pub fn function_with_labeled_parameters_test() {
  let mod = {
    use _sum_of_2_numbers <- module.with_function(
      definition.new(name: "sum_of_2_numbers")
        |> definition.with_publicity(True),
      function.new2(
        param1: parameter.new("num1", types.int)
          |> parameter.with_label("first"),
        param2: parameter.new("num2", types.int)
          |> parameter.with_label("second"),
        returns: types.int,
        handler: fn(num1, num2) {
          expression.math_operator(num1, expression.Add, num2)
        },
      ),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "pub fn sum_of_2_numbers(first num1: Int, second num2: Int) -> Int {
  num1 + num2
}"

  assert result == expected
}

pub fn function_with_auto_inserting_labeled_parameters_test() {
  let mod = {
    use _sum_of_2_numbers <- module.with_function(
      definition.new(name: "sum_of_2_numbers")
        |> definition.with_publicity(True),
      function.new2(
        param1: parameter.new("num1", types.int)
          |> parameter.with_label("first"),
        param2: parameter.new("num2", types.int),
        returns: types.int,
        handler: fn(num1, num2) {
          expression.math_operator(num1, expression.Add, num2)
        },
      ),
    )

    module.eof()
  }

  let rendered = module.render(mod, render.default_context())
  let result = render.to_string(rendered)

  let expected =
    "pub fn sum_of_2_numbers(first num1: Int, num2 num2: Int) -> Int {
  num1 + num2
}"

  assert result == expected
  assert rendered.details.report.warnings
    == [
      report.AutomaticallyAddedMissingLabels(["num2"]),
    ]
}

pub fn function_notice_unlabeled_parameters_test() {
  let mod = {
    use _list_of_6_numbers <- module.with_function(
      definition.new(name: "list_of_6_numbers")
        |> definition.with_publicity(True),
      function.new6(
        param1: parameter.new("num1", types.int),
        param2: parameter.new("num2", types.int)
          |> parameter.with_label("second"),
        param3: parameter.new("num3", types.int),
        param4: parameter.new("num4", types.int),
        param5: parameter.new("num5", types.int)
          |> parameter.with_label("fifth"),
        param6: parameter.new("num6", types.int),
        returns: types.list(types.int),
        handler: fn(num1, num2, num3, num4, num5, num6) {
          expression.list([
            num1,
            num2,
            num3,
            num4,
            num5,
            num6,
          ])
        },
      ),
    )

    module.eof()
  }

  let config =
    render.context_from_config(
      config.Config(..config.default_config, auto_fix_parameters: False),
    )

  let rendered = module.render(mod, config)
  let result = render.to_string(rendered)

  let expected =
    "pub fn list_of_6_numbers(
  num1: Int,
  second num2: Int,
  num3: Int,
  num4: Int,
  fifth num5: Int,
  num6: Int,
) -> List(Int) {
  [num1, num2, num3, num4, num5, num6]
}"

  assert result == expected
  assert rendered.details.report.errors
    == [
      report.MissingLabels(["num3", "num4", "num6"]),
    ]
}

pub fn anonymous_functions_ignore_labels_test() {
  let add_function =
    function.new2(
      param1: parameter.new("num1", types.int)
        |> parameter.with_label("first"),
      param2: parameter.new("num2", types.int)
        |> parameter.with_label("second"),
      returns: types.int,
      handler: fn(num1, num2) {
        expression.math_operator(num1, expression.Add, num2)
      },
    )
  let mod = {
    use _sum_of_2_numbers <- module.with_function(
      definition.new(name: "sum_of_2_numbers")
        |> definition.with_publicity(True),
      add_function,
    )

    use _sum_of_2_and_3 <- module.with_function(
      definition.new(name: "sum_of_2_and_3")
        |> definition.with_publicity(True),
      function.new0(types.int, fn() {
        expression.call2(
          function.anonymous(add_function),
          expression.int(2),
          expression.int(3),
        )
      }),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "pub fn sum_of_2_numbers(first num1: Int, second num2: Int) -> Int {
  num1 + num2
}

pub fn sum_of_2_and_3() -> Int {
  fn(num1: Int, num2: Int) -> Int { num1 + num2 }(2, 3)
}"

  assert result == expected
}

pub fn module_with_function_test() {
  let mod = {
    use io <- module.with_import(import_.new(["gleam", "io"]))
    use language <- module.with_constant(
      definition.new("language"),
      expression.string("gleam"),
    )

    use describer <- module.with_function(
      definition.new(name: "describer")
        |> definition.with_publicity(True)
        |> definition.with_attributes([definition.Internal]),
      function.new1(
        param1: parameter.new("thing", types.string),
        returns: types.string,
        handler: fn(thing) {
          expression.string("The ")
          |> expression.concat_string(thing)
          |> expression.concat_string(expression.string(" is written in "))
          |> expression.concat_string(language)
        },
      ),
    )

    use _main <- module.with_function(
      definition.new(name: "main")
        |> definition.with_publicity(True),
      function.new0(returns: types.nil, handler: fn() {
        expression.call1(
          import_.raw_ident(io, "println"),
          expression.call1(describer, expression.string("program")),
        )
      }),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/io

const language = \"gleam\"

@internal
pub fn describer(thing: String) -> String {
  \"The \" <> thing <> \" is written in \" <> language
}

pub fn main() -> Nil {
  io.println(describer(\"program\"))
}"

  assert result == expected
}

pub fn module_import_constructor_test() {
  let mod = {
    use option_module <- module.with_import(import_.new(["gleam", "option"]))

    let option_type = import_.raw_type(option_module, "Option")

    use _option_from_str <- module.with_function(
      definition.new(name: "option_from_string")
        |> definition.with_publicity(True),
      function.new1(
        param1: parameter.new("str", types.string),
        returns: option_type |> custom.to_type1(types.string),
        handler: fn(str) {
          case_.new(str)
          |> case_.with_pattern(pattern.string_literal(""), fn(_) {
            expression.construct0(import_.value_of_type(
              option_module,
              "None",
              types.function0(option_type |> custom.to_type1(types.string)),
            ))
          })
          |> case_.with_pattern(pattern.variable("value"), fn(value) {
            expression.construct1(
              import_.value_of_type(
                option_module,
                "Some",
                types.function1(
                  types.string,
                  option_type |> custom.to_type1(types.string),
                ),
              ),
              value,
            )
          })
          |> case_.build_expression()
        },
      ),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/option

pub fn option_from_string(str: String) -> option.Option(String) {
  case str {
    \"\" -> option.None
    value -> option.Some(value)
  }
}"

  assert result == expected
}

pub fn basic_use_test() {
  let mod = {
    use result_module <- module.with_import(import_.new(["gleam", "result"]))

    use _ <- module.with_function(
      definition.new(name: "do_result")
        |> definition.with_publicity(True),
      function.new0(
        returns: types.result(types.int, types.string),
        handler: fn() {
          use res <- block.with_let_declaration(
            "res",
            expression.ok(expression.int(3)),
          )
          use ok_value <- block.with_use1(
            block.use_function1(
              import_.function2(result_module, result.try),
              res,
            ),
            "ok_value",
          )

          ok_value
          |> expression.math_operator(expression.Add, expression.int(5))
          |> expression.ok
        },
      ),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/result

pub fn do_result() -> Result(Int, String) {
  let res = Ok(3)
  use ok_value <- result.try(res)
  Ok(ok_value + 5)
}"
  assert result == expected
}

pub fn two_use_test() {
  let mod = {
    use result_module <- module.with_import(import_.new(["gleam", "result"]))
    use bool_module <- module.with_import(import_.new(["gleam", "bool"]))

    use _ <- module.with_function(
      definition.new(name: "do_result")
        |> definition.with_publicity(True),
      function.new0(
        returns: types.result(types.int, types.string),
        handler: fn() {
          use res <- block.with_let_declaration(
            "res",
            expression.ok(expression.int(3)),
          )

          use ok_value <- block.with_use1(
            block.use_function1(
              import_.function2(result_module, result.try),
              res,
            ),
            "ok_value",
          )

          use <- block.with_use0(block.use_function2(
            import_.function3(bool_module, bool.guard),
            expression.equals(ok_value, expression.int(2)),
            expression.error(expression.string("not equal to 2")),
          ))

          ok_value
          |> expression.math_operator(expression.Add, expression.int(5))
          |> expression.ok
        },
      ),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/bool
import gleam/result

pub fn do_result() -> Result(Int, String) {
  let res = Ok(3)
  use ok_value <- result.try(res)
  use <- bool.guard(ok_value == 2, Error(\"not equal to 2\"))
  Ok(ok_value + 5)
}"
  assert result == expected
}

/// Regression test for single-expression blocks in `let` values (`render_block`).
///
/// A block value that is only one expression omits redundant `{ ... }`
/// (e.g. `let y = x + 1`, not `let y = { x + 1 }`).
pub fn single_expression_block_in_let_value_test() {
  let value =
    block.new_dynamic([
      statement.expression(expression.math_operator(
        expression.raw("x"),
        expression.Add,
        expression.int(1),
      )),
    ])

  let result =
    block.new_dynamic([
      statement.dynamic_let("y", value, False),
      statement.expression(expression.raw("y")),
    ])
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "{
  let y = x + 1
  y
}"

  assert result == expected
}

/// Regression test for rendering block arguments in call expressions.
///
/// Ensures calls like `result.or(...)` keep braces for block arguments with
/// `let` statements, while still unwrapping single-expression blocks (via
/// `render_block`).
pub fn call_with_block_argument_test() {
  let with_let =
    expression.call_dynamic(expression.raw("result.or"), [
      expression.ok(expression.int(3)) |> expression.to_dynamic(),
      block.with_let_declaration("next", expression.int(4), fn(next) {
        expression.ok(next)
      })
        |> expression.to_dynamic(),
    ])
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected_with_let =
    "result.or(Ok(3), {
    let next = 4
    Ok(next)
  })"

  assert with_let == expected_with_let

  let direct_return =
    expression.call_dynamic(expression.raw("result.or"), [
      expression.ok(expression.int(3)) |> expression.to_dynamic(),
      block.new_dynamic([statement.expression(expression.ok(expression.int(4)))]),
    ])
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected_direct_return = "result.or(Ok(3), Ok(4))"

  assert direct_return == expected_direct_return
}

/// Regression test for list pattern helpers with zero-argument constructors.
///
/// Ensures `pattern.list_empty()` renders as `[]` (not `[]()`), and that a
/// variable pattern matches a non-empty list branch after the empty list arm.
pub fn case_with_list_empty_and_spread_pattern_test() {
  let result =
    case_.new(expression.list([]))
    |> case_.with_pattern(pattern.list_empty(), fn(_) {
      expression.string("empty")
    })
    |> case_.with_pattern(pattern.variable("items"), fn(_) {
      expression.string("not empty")
    })
    |> case_.build_expression()
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "case [] {
  [] -> \"empty\"
  items -> \"not empty\"
}"

  assert result == expected
}

/// Regression test for option pattern helper rendering.
///
/// Ensures `pattern.option_some(...)` renders `Some(...)` and
/// `pattern.option_none()` renders `None` (no zero-arg parentheses).
pub fn case_with_option_pattern_helpers_test() {
  let result =
    case_.new(expression.raw("maybe_name"))
    |> case_.with_pattern(
      pattern.option_some(pattern.variable("name")),
      fn(name) { expression.concat_string(name, expression.string("!")) },
    )
    |> case_.with_pattern(pattern.option_none(), fn(_) {
      expression.string("anonymous")
    })
    |> case_.build_expression()
    |> expression.render(render.default_context())
    |> render.to_string()

  let expected =
    "case maybe_name {
  Some(name) -> name <> \"!\"
  None -> \"anonymous\"
}"

  assert result == expected
}

pub fn result_test() {
  let mod = {
    use result_module <- module.with_import(import_.new(["gleam", "result"]))
    use string_module <- module.with_import(import_.new(["gleam", "string"]))

    use _swap_result <- module.with_function(
      definition.new(name: "handle_result")
        |> definition.with_publicity(True),
      function.new1(
        param1: parameter.new("res", types.result(types.string, types.int)),
        returns: types.result(types.bool, types.int),
        handler: fn(res) {
          use _ <- block.with_let_declaration(
            "v",
            expression.call2(
              import_.function2(result_module, result.unwrap),
              expression.ok(expression.string("hi")),
              expression.string("hey"),
            ),
          )

          let special_pattern =
            pattern.or(
              pattern.ok(pattern.string_literal("")),
              pattern.error(pattern.int_literal(0)),
            )

          case_.new(res)
          |> case_.with_pattern(special_pattern, fn(_) {
            expression.ok(expression.bool(True))
          })
          |> case_.with_pattern(pattern.ok(pattern.variable("str")), fn(str) {
            expression.call1(
              import_.function1(string_module, string.length),
              str,
            )
            |> expression.error()
          })
          |> case_.with_pattern(
            pattern.error(pattern.variable("number")),
            fn(number) { expression.error(number) },
          )
          |> case_.build_expression()
        },
      ),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/result
import gleam/string

pub fn handle_result(res: Result(String, Int)) -> Result(Bool, Int) {
  let v = result.unwrap(Ok(\"hi\"), \"hey\")
  case res {
    Ok(\"\") | Error(0) -> Ok(True)
    Ok(str) -> Error(string.length(str))
    Error(number) -> Error(number)
  }
}"
  assert result == expected
}

pub fn module_with_type_alias_test() {
  let mod = {
    use awesome_string <- module.with_type_alias(
      definition.new(name: "AwesomeString"),
      types.string,
    )

    use _ <- module.with_function(
      definition.new("runner")
        |> definition.with_publicity(True)
        |> definition.with_attributes([definition.Internal]),
      function.new1(
        param1: parameter.new("thing", awesome_string),
        returns: types.string,
        handler: fn(thing) {
          expression.string("Hi ")
          |> expression.concat_string(thing)
        },
      ),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "type AwesomeString = String

@internal
pub fn runner(thing: AwesomeString) -> String {
  \"Hi \" <> thing
}"

  assert result == expected
}

pub fn module_import_test() {
  let mod = {
    use io <- module.with_import(
      import_.new(["gleam", "io"]) |> import_.with_alias("only_o"),
    )
    use int_mod <- module.with_import(import_.new(["gleam", "int"]))

    let io_print = import_.function1(io, io.println)
    let int_string =
      import_.value_of_type(
        int_mod,
        "to_string",
        types.reference(int.to_string),
      )

    use _main <- module.with_function(
      definition.new(name: "main")
        |> definition.with_publicity(True),
      function.new0(returns: types.nil, handler: fn() {
        expression.call1(
          io_print,
          expression.call1(int_string, expression.int(23)),
        )
      }),
    )
    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/int
import gleam/io as only_o

pub fn main() -> Nil {
  only_o.println(int.to_string(23))
}"

  assert result == expected
}

/// Regression test for `import_.with_exposing`: rendered `import path.{items}` and kept in output
/// when nothing references the module prefix (only unqualified imports from the exposing list).
pub fn module_import_with_exposing_test() {
  let mod = {
    use _string <- module.with_import(
      import_.new(["gleam", "string"])
      |> import_.with_exposing([import_.exposed_value("length")]),
    )

    use _main <- module.with_function(
      definition.new(name: "main")
        |> definition.with_publicity(True),
      function.new0(returns: types.nil, handler: fn() { expression.nil() }),
    )
    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/string.{length}

pub fn main() -> Nil {
  Nil
}"

  assert result == expected
}

/// `import path.{items} as alias` — exposing must come before `as` in Gleam syntax.
pub fn module_import_with_alias_and_exposing_test() {
  let mod = {
    use io <- module.with_import(
      import_.new(["gleam", "io"])
      |> import_.with_exposing([import_.exposed_value("println")])
      |> import_.with_alias("only_o"),
    )

    let io_print = import_.function1(io, io.println)

    use _main <- module.with_function(
      definition.new(name: "main")
        |> definition.with_publicity(True),
      function.new0(returns: types.nil, handler: fn() {
        expression.call1(io_print, expression.string("hi"))
      }),
    )
    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/io.{println} as only_o

pub fn main() -> Nil {
  only_o.println(\"hi\")
}"

  assert result == expected
}

/// Duplicate module paths with separate exposing lists merge into one import (sorted, deduped).
pub fn module_merge_imports_exposing_test() {
  let mod = {
    use _ <- module.with_import(
      import_.new(["gleam", "string"])
      |> import_.with_exposing([import_.exposed_value("reverse")]),
    )
    use _ <- module.with_import(
      import_.new(["gleam", "string"])
      |> import_.with_exposing([import_.exposed_value("length")]),
    )

    use _main <- module.with_function(
      definition.new(name: "main")
        |> definition.with_publicity(True),
      function.new0(returns: types.nil, handler: fn() { expression.nil() }),
    )
    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/string.{length, reverse}

pub fn main() -> Nil {
  Nil
}"

  assert result == expected
}

/// Merging imports with overlapping exposing lists deduplicates entries.
pub fn module_merge_imports_exposing_dedupes_test() {
  let mod = {
    use _ <- module.with_import(
      import_.new(["gleam", "string"])
        |> import_.with_exposing([import_.exposed_value("length")]),
    )
    use _ <- module.with_import(
      import_.new(["gleam", "string"])
        |> import_.with_exposing([import_.exposed_value("length")]),
    )

    use _main <- module.with_function(
      definition.new(name: "main")
        |> definition.with_publicity(True),
      function.new0(returns: types.nil, handler: fn() { expression.nil() }),
    )
    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/string.{length}

pub fn main() -> Nil {
  Nil
}"

  assert result == expected
}

pub fn module_unused_import_test() {
  let mod = {
    use io <- module.with_import(
      import_.new(["gleam", "io"]) |> import_.with_alias("only_o"),
    )
    use int_mod <- module.with_import(import_.new(["gleam", "int"]))
    use _ <- module.with_import(import_.new(["gleam", "string"]))

    let io_print = import_.function1(io, io.println)
    let int_string =
      import_.value_of_type(
        int_mod,
        "to_string",
        types.reference(int.to_string),
      )

    use _main <- module.with_function(
      definition.new(name: "main")
        |> definition.with_publicity(True),
      function.new0(returns: types.nil, handler: fn() {
        expression.call1(
          io_print,
          expression.call1(int_string, expression.int(23)),
        )
      }),
    )
    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/int
import gleam/io as only_o

pub fn main() -> Nil {
  only_o.println(int.to_string(23))
}"

  assert result == expected
}

// Because this is False, we don't use the string module, 
// therefore it should not be rendered in the final string
const use_string_mod_this_time = False

pub fn module_sometimes_unused_import_test() {
  let mod = {
    use io <- module.with_import(
      import_.new(["gleam", "io"]) |> import_.with_alias("only_o"),
    )
    use int_mod <- module.with_import(import_.new(["gleam", "int"]))
    use string_mod <- module.with_import(import_.new(["gleam", "string"]))

    let io_print = import_.function1(io, io.println)
    let int_string =
      import_.value_of_type(
        int_mod,
        "to_string",
        types.reference(int.to_string),
      )
    let string_length = import_.function1(string_mod, string.length)

    let int_value = case use_string_mod_this_time {
      True -> expression.call1(string_length, expression.string("hi"))
      False -> expression.int(23)
    }

    use _main <- module.with_function(
      definition.new(name: "main")
        |> definition.with_publicity(True),
      function.new0(returns: types.nil, handler: fn() {
        expression.call1(io_print, expression.call1(int_string, int_value))
      }),
    )
    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/int
import gleam/io as only_o

pub fn main() -> Nil {
  only_o.println(int.to_string(23))
}"

  assert result == expected
}

pub type ExampleAnimal {
  ExampleAnimal
}

pub fn module_with_custom_type_test() {
  let animals =
    custom.new(ExampleAnimal)
    |> custom.with_variant(fn(_) {
      variant.new("Dog")
      |> variant.with_argument(option.Some("bones"), types.int)
    })
    |> custom.with_variant(fn(_) {
      variant.new("Cat")
      |> variant.with_argument(option.Some("name"), types.string)
      |> variant.with_argument(option.Some("has_catnip"), types.bool)
    })

  let mod = {
    use animal_type, dog_constructor, cat_constructor <- module.with_custom_type2(
      definition.new("Animal") |> definition.with_publicity(True),
      animals,
    )

    use describer <- module.with_function(
      definition.new("describer") |> definition.with_publicity(True),
      function.new1(
        param1: parameter.new("animal", animal_type |> custom.to_type()),
        returns: types.string,
        handler: fn(_thing) { expression.todo_(option.Some("implement me")) },
      ),
    )

    use _main <- module.with_function(
      definition.new(name: "main")
        |> definition.with_publicity(True),
      function.new0(returns: types.nil, handler: fn() {
        {
          use dog_var <- block.with_let_declaration(
            "dog",
            expression.construct1(
              constructor.to_expression1(dog_constructor),
              expression.int(4),
            ),
          )
          use <- block.with_expression(expression.call1(describer, dog_var))
          use cat_var <- block.with_let_declaration(
            "cat",
            expression.construct2(
              constructor.to_expression2(cat_constructor),
              expression.string("jake"),
              expression.bool(True),
            ),
          )
          use <- block.with_expression(expression.call1(describer, cat_var))
          expression.nil()
        }
      }),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "pub type Animal {
  Dog(bones: Int)
  Cat(name: String, has_catnip: Bool)
}

pub fn describer(animal: Animal) -> String {
  todo as \"implement me\"
}

pub fn main() -> Nil {
  let dog = Dog(4)
  describer(dog)
  let cat = Cat(\"jake\", True)
  describer(cat)
  Nil
}"

  assert result == expected
}

pub fn module_case_on_custom_type_test() {
  let animals =
    custom.new(ExampleAnimal)
    |> custom.with_variant(fn(_) {
      variant.new("Dog")
      |> variant.with_argument(option.Some("bones"), types.int)
    })
    |> custom.with_variant(fn(_) {
      variant.new("Cat")
      |> variant.with_argument(option.Some("name"), types.string)
      |> variant.with_argument(option.Some("has_catnip"), types.bool)
    })

  let mod = {
    use int_mod <- module.with_import(import_.new(["gleam", "int"]))
    use animal_type, dog_constructor, cat_constructor <- module.with_custom_type2(
      definition.new("Animal") |> definition.with_publicity(True),
      animals,
    )

    let int_to_string =
      import_.value_of_type(
        int_mod,
        "to_string",
        types.reference(int.to_string),
      )

    use describer <- module.with_function(
      definition.new("describer") |> definition.with_publicity(True),
      function.new1(
        param1: parameter.new("animal", animal_type |> custom.to_type()),
        returns: types.string,
        handler: fn(animal) {
          case_.new(animal)
          |> case_.with_pattern(
            pattern.from_constructor1(
              dog_constructor,
              pattern.variable("bones"),
            ),
            fn(bones) {
              expression.string("Dog with ")
              |> expression.concat_string(expression.call1(int_to_string, bones))
            },
          )
          |> case_.with_pattern(
            pattern.from_constructor2(
              cat_constructor,
              pattern.variable("name"),
              pattern.bool_literal(True),
            ),
            fn(info) {
              let #(name, Nil) = info
              expression.string("Cat named ")
              |> expression.concat_string(name)
              |> expression.concat_string(expression.string(" (energetic!)"))
            },
          )
          |> case_.with_pattern(
            pattern.from_constructor2(
              cat_constructor,
              pattern.variable("name"),
              pattern.bool_literal(False),
            ),
            fn(info) {
              let #(name, Nil) = info
              expression.string("Bored cat named ")
              |> expression.concat_string(name)
            },
          )
          |> case_.build_expression()
        },
      ),
    )

    use _main <- module.with_function(
      definition.new(name: "main")
        |> definition.with_publicity(True),
      function.new0(returns: types.nil, handler: fn() {
        {
          use dog_var <- block.with_let_declaration(
            "dog",
            expression.call1(
              constructor.to_expression1(dog_constructor),
              expression.int(4),
            ),
          )
          use <- block.with_expression(expression.call1(describer, dog_var))
          use cat_var <- block.with_let_declaration(
            "cat",
            expression.call2(
              constructor.to_expression2(cat_constructor),
              expression.string("jake"),
              expression.bool(True),
            ),
          )
          use <- block.with_expression(expression.call1(describer, cat_var))
          expression.nil()
        }
      }),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/int

pub type Animal {
  Dog(bones: Int)
  Cat(name: String, has_catnip: Bool)
}

pub fn describer(animal: Animal) -> String {
  case animal {
    Dog(bones) -> \"Dog with \" <> int.to_string(bones)
    Cat(name, True) -> \"Cat named \" <> name <> \" (energetic!)\"
    Cat(name, False) -> \"Bored cat named \" <> name
  }
}

pub fn main() -> Nil {
  let dog = Dog(4)
  describer(dog)
  let cat = Cat(\"jake\", True)
  describer(cat)
  Nil
}"

  assert result == expected
}

pub fn module_let_on_custom_type_test() {
  let animals =
    custom.new(ExampleAnimal)
    |> custom.with_variant(fn(_) {
      variant.new("Dog")
      |> variant.with_argument(option.Some("bones"), types.int)
    })
    |> custom.with_variant(fn(_) {
      variant.new("Cat")
      |> variant.with_argument(option.Some("name"), types.string)
      |> variant.with_argument(option.Some("has_catnip"), types.bool)
    })

  let mod = {
    use int_mod <- module.with_import(import_.new(["gleam", "int"]))
    use _animal_type, dog_constructor, cat_constructor <- module.with_custom_type2(
      definition.new("Animal") |> definition.with_publicity(True),
      animals,
    )

    let int_to_string =
      import_.value_of_type(
        int_mod,
        "to_string",
        types.reference(int.to_string),
      )

    use _describe <- module.with_function(
      definition.new("describer") |> definition.with_publicity(True),
      function.new0(returns: types.string, handler: fn() {
        use bones <- block.with_matching_let_declaration(
          pattern.from_constructor1(dog_constructor, pattern.variable("bones")),
          expression.construct1(
            constructor.to_expression1(dog_constructor),
            expression.int(4),
          ),
          True,
        )

        use #(name, Nil) <- block.with_matching_let_declaration(
          pattern.from_constructor2(
            cat_constructor,
            pattern.as_(pattern.string_literal("jake"), "name"),
            pattern.bool_literal(True),
          ),
          expression.construct2(
            constructor.to_expression2(cat_constructor),
            expression.string("jake"),
            expression.bool(True),
          ),
          True,
        )
        expression.concat_string(
          expression.concat_string(
            name,
            expression.string(" knows a dog with this many bones: "),
          ),
          expression.call1(int_to_string, bones),
        )
      }),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/int

pub type Animal {
  Dog(bones: Int)
  Cat(name: String, has_catnip: Bool)
}

pub fn describer() -> String {
  let assert Dog(bones) = Dog(4)
  let assert Cat(\"jake\" as name, True) = Cat(\"jake\", True)
  name <> \" knows a dog with this many bones: \" <> int.to_string(bones)
}"

  assert result == expected
}

pub fn module_with_custom_type_generics_test() {
  let more_awesome_result: custom.CustomTypeBuilder(Nil, _, _) =
    custom.new(Nil)
    |> custom.with_generic("awesome")
    |> custom.with_generic("not_awesome")
    |> custom.with_variant(fn(generics) {
      let #(#(#(), awesome), _not_awesome) = generics
      variant.new("VeryOk")
      |> variant.with_argument(option.Some("contents"), awesome)
    })
    |> custom.with_variant(fn(generics) {
      let #(#(#(), awesome), not_awesome) = generics
      variant.new("NotVeryOk")
      |> variant.with_argument(option.Some("contents"), awesome)
      |> variant.with_argument(option.Some("failures"), not_awesome)
    })

  let mod = {
    use awesome_type, ok_awesome_constructor, less_ok_awesome_constructor <- module.with_custom_type2(
      definition.new(name: "MoreAwesomeResult")
        |> definition.with_publicity(True),
      more_awesome_result,
    )

    use _main <- module.with_function(
      definition.new(name: "generate") |> definition.with_publicity(True),
      function.new0(
        returns: custom.to_type2(awesome_type, types.int, types.bool),
        handler: fn() {
          use _ <- block.with_let_declaration(
            "whoo",
            expression.call1(
              constructor.to_expression1(ok_awesome_constructor),
              expression.int(4),
            ),
          )
          expression.call2(
            constructor.to_expression2(less_ok_awesome_constructor),
            expression.int(23),
            expression.bool(True),
          )
        },
      ),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "pub type MoreAwesomeResult(awesome, not_awesome) {
  VeryOk(contents: awesome)
  NotVeryOk(contents: awesome, failures: not_awesome)
}

pub fn generate() -> MoreAwesomeResult(Int, Bool) {
  let whoo = VeryOk(4)
  NotVeryOk(23, True)
}"

  assert result == expected
}

pub fn module_with_custom_type_generics_multiple_ways_test() {
  let more_awesome_result: custom.CustomTypeBuilder(
    Nil,
    _,
    custom.Generics2(types.GeneratedType(a), types.GeneratedType(b)),
  ) =
    custom.new(Nil)
    |> custom.with_generic("awesome")
    |> custom.with_generic("not_awesome")
    |> custom.with_variant(fn(generics) {
      let #(#(#(), awesome), _not_awesome) = generics
      variant.new("VeryOk")
      |> variant.with_argument(option.Some("contents"), awesome)
    })
    |> custom.with_variant(fn(generics) {
      let #(#(#(), awesome), not_awesome) = generics
      variant.new("NotVeryOk")
      |> variant.with_argument(option.Some("contents"), awesome)
      |> variant.with_argument(option.Some("failures"), not_awesome)
    })

  let mod = {
    use awesome_type, base_ok_constructor, base_less_ok_constructor <- module.with_custom_type2(
      definition.new(name: "MoreAwesomeResult")
        |> definition.with_publicity(True),
      more_awesome_result,
    )

    let first_ok_constructor: constructor.Constructor(
      Nil,
      _,
      custom.Generics2(types.GeneratedType(Int), types.GeneratedType(String)),
    ) = constructor.unsafe_convert(base_ok_constructor)

    let less_ok_constructor: constructor.Constructor(
      Nil,
      _,
      custom.Generics2(types.GeneratedType(String), types.GeneratedType(Bool)),
    ) = constructor.unsafe_convert(base_less_ok_constructor)

    use _main <- module.with_function(
      definition.new(name: "generate")
        |> definition.with_publicity(True),
      function.new0(
        returns: custom.to_type2(awesome_type, types.string, types.bool),
        handler: fn() {
          use _ <- block.with_let_declaration(
            "whoo",
            expression.call1(
              constructor.to_expression1(first_ok_constructor),
              expression.int(4),
            ),
          )
          expression.call2(
            constructor.to_expression2(less_ok_constructor),
            expression.string("hi"),
            expression.bool(True),
          )
        },
      ),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "pub type MoreAwesomeResult(awesome, not_awesome) {
  VeryOk(contents: awesome)
  NotVeryOk(contents: awesome, failures: not_awesome)
}

pub fn generate() -> MoreAwesomeResult(String, Bool) {
  let whoo = VeryOk(4)
  NotVeryOk(\"hi\", True)
}"

  assert result == expected
}

pub fn case_unchecked_variant_test() {
  let custom_variant =
    variant.new("CustomVariant")
    |> variant.with_arguments_dynamic(
      list.range(0, 10)
      |> list.map(fn(x) {
        #(
          option.Some("arg" <> int.to_string(x)),
          types.int |> types.to_dynamic(),
        )
      }),
    )
    |> variant.to_dynamic()

  let custom_type =
    custom.new(#())
    |> custom.with_dynamic_variants(fn(_) { [custom_variant] })

  let mod = {
    use int_module <- module.with_import(import_.new(["gleam", "int"]))

    use _, custom_constructors <- module.with_custom_type_dynamic(
      definition.new(name: "VariantHolder") |> definition.with_publicity(True),
      custom_type,
    )
    let assert [custom_variant, ..] = custom_constructors

    let match_on =
      expression.call_dynamic(
        constructor.to_expression_dynamic(custom_variant),
        list.range(0, 15)
          |> list.map(fn(x) { expression.int(x + 4) |> expression.to_dynamic() }),
      )

    use _ <- module.with_function(
      definition.new(name: "handle")
        |> definition.with_publicity(True),
      function.new0(returns: types.int, handler: fn() {
        case_.new(match_on)
        |> case_.with_pattern(
          pattern.from_constructor_dynamic(
            custom_variant,
            list.range(0, 15)
              |> list.map(fn(x) {
                case x % 2 {
                  0 ->
                    pattern.int_literal(x + 4)
                    |> pattern.to_dynamic()
                  _ ->
                    pattern.variable("value" <> int.to_string(x))
                    |> pattern.to_dynamic()
                }
              }),
          ),
          fn(details) {
            expression.call1(
              import_.function1(int_module, int.sum),
              expression.list(details)
                |> expression.coerce_dynamic_unsafe(),
            )
          },
        )
        |> case_.with_pattern(pattern.variable("v"), fn(_) {
          // expression.concat_string(v, expression.string(" world"))
          expression.int(5)
        })
        |> case_.build_expression()
      }),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "import gleam/int

pub type VariantHolder {
  CustomVariant(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
    arg7: Int,
    arg8: Int,
    arg9: Int,
    arg10: Int,
  )
}

pub fn handle() -> Int {
  case CustomVariant(4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18, 19) {
    CustomVariant(
      4,
      value1,
      6,
      value3,
      8,
      value5,
      10,
      value7,
      12,
      value9,
      14,
      value11,
      16,
      value13,
      18,
      value15,
    )
    ->
    int.sum([value1, value3, value5, value7, value9, value11, value13, value15])
    v -> 5
  }
}"

  assert result == expected
}

pub fn module_with_unchecked_custom_types_test() {
  let all_variants =
    list.range(0, 20)
    |> list.map(fn(i) {
      variant.new("Variant" <> int.to_string(i))
      |> variant.with_arguments_dynamic(
        list.range(0, i)
        |> list.reverse()
        |> list.rest()
        |> result.unwrap([])
        |> list.reverse()
        |> list.map(fn(x) {
          #(
            option.Some("arg" <> int.to_string(x)),
            types.int |> types.to_dynamic(),
          )
        }),
      )
      |> variant.to_dynamic()
    })

  let custom_type =
    custom.new(#())
    |> custom.with_dynamic_variants(fn(_) { all_variants })

  let mod = {
    use custom_type_type, custom_constructors <- module.with_custom_type_dynamic(
      definition.new(name: "VariantHolder")
        |> definition.with_publicity(True),
      custom_type,
    )

    let assert [variant0, _, _, variant3, ..] = custom_constructors

    use _get_variant <- module.with_function(
      definition.new(name: "get_variant")
        |> definition.with_publicity(True),
      function.new0(returns: custom.to_type(custom_type_type), handler: fn() {
        constructor.to_expression_dynamic(variant0)
      }),
    )
    use _get_other_variant <- module.with_function(
      definition.new(name: "get_other_variant")
        |> definition.with_publicity(True),
      function.new0(returns: custom.to_type(custom_type_type), handler: fn() {
        expression.call3(
          constructor.to_expression_dynamic(variant3),
          expression.int(1),
          expression.int(2),
          expression.int(3),
        )
      }),
    )

    module.eof()
  }

  let result =
    mod
    |> module.render(render.default_context())
    |> render.to_string()

  let expected =
    "pub type VariantHolder {
  Variant0
  Variant1(arg0: Int)
  Variant2(arg0: Int, arg1: Int)
  Variant3(arg0: Int, arg1: Int, arg2: Int)
  Variant4(arg0: Int, arg1: Int, arg2: Int, arg3: Int)
  Variant5(arg0: Int, arg1: Int, arg2: Int, arg3: Int, arg4: Int)
  Variant6(arg0: Int, arg1: Int, arg2: Int, arg3: Int, arg4: Int, arg5: Int)
  Variant7(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
  )
  Variant8(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
    arg7: Int,
  )
  Variant9(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
    arg7: Int,
    arg8: Int,
  )
  Variant10(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
    arg7: Int,
    arg8: Int,
    arg9: Int,
  )
  Variant11(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
    arg7: Int,
    arg8: Int,
    arg9: Int,
    arg10: Int,
  )
  Variant12(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
    arg7: Int,
    arg8: Int,
    arg9: Int,
    arg10: Int,
    arg11: Int,
  )
  Variant13(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
    arg7: Int,
    arg8: Int,
    arg9: Int,
    arg10: Int,
    arg11: Int,
    arg12: Int,
  )
  Variant14(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
    arg7: Int,
    arg8: Int,
    arg9: Int,
    arg10: Int,
    arg11: Int,
    arg12: Int,
    arg13: Int,
  )
  Variant15(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
    arg7: Int,
    arg8: Int,
    arg9: Int,
    arg10: Int,
    arg11: Int,
    arg12: Int,
    arg13: Int,
    arg14: Int,
  )
  Variant16(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
    arg7: Int,
    arg8: Int,
    arg9: Int,
    arg10: Int,
    arg11: Int,
    arg12: Int,
    arg13: Int,
    arg14: Int,
    arg15: Int,
  )
  Variant17(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
    arg7: Int,
    arg8: Int,
    arg9: Int,
    arg10: Int,
    arg11: Int,
    arg12: Int,
    arg13: Int,
    arg14: Int,
    arg15: Int,
    arg16: Int,
  )
  Variant18(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
    arg7: Int,
    arg8: Int,
    arg9: Int,
    arg10: Int,
    arg11: Int,
    arg12: Int,
    arg13: Int,
    arg14: Int,
    arg15: Int,
    arg16: Int,
    arg17: Int,
  )
  Variant19(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
    arg7: Int,
    arg8: Int,
    arg9: Int,
    arg10: Int,
    arg11: Int,
    arg12: Int,
    arg13: Int,
    arg14: Int,
    arg15: Int,
    arg16: Int,
    arg17: Int,
    arg18: Int,
  )
  Variant20(
    arg0: Int,
    arg1: Int,
    arg2: Int,
    arg3: Int,
    arg4: Int,
    arg5: Int,
    arg6: Int,
    arg7: Int,
    arg8: Int,
    arg9: Int,
    arg10: Int,
    arg11: Int,
    arg12: Int,
    arg13: Int,
    arg14: Int,
    arg15: Int,
    arg16: Int,
    arg17: Int,
    arg18: Int,
    arg19: Int,
  )
}

pub fn get_variant() -> VariantHolder {
  Variant0
}

pub fn get_other_variant() -> VariantHolder {
  Variant3(1, 2, 3)
}"

  assert result == expected
}
